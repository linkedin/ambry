// Copyright (C) 2014-2016 LinkedIn Corp. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License"); you may not use
// this file except in compliance with the License. You may obtain a copy of the
// License at  http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied.

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

apply plugin: 'org.shipkit.shipkit-auto-version'
println "Building version $version"

apply from: file('gradle/license.gradle')
apply from: file('gradle/environment.gradle')
apply from: file("gradle/dependency-versions.gradle")
if (!project.hasProperty('disableShipkit')) {
    apply from: file("gradle/ci-release.gradle")
}

allprojects {
    group = "com.github.ambry"

    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'project-report'
    apply plugin: 'jacoco'
    apply plugin: 'org.gradle.test-retry'

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java'

    if (!project.hasProperty('disableShipkit')) {
        apply from: "$rootDir/gradle/java-publishing.gradle"
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    if (JavaVersion.current().java9Compatible) {
        // Our consumers still run on java 8, so this flag ensures that our builds are backwards compatible at runtime
        // with java 8 platform APIs.
        compileJava {
            options.compilerArgs.addAll(['--release', '8'])
        }
    }

    sourceSets {
        // separate source set for integration tests
        intTest {
            java.srcDir file('src/integration-test/java')
            resources.srcDir file('src/integration-test/resources')
            compileClasspath += sourceSets.main.output + sourceSets.test.output
            runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        }
    }

    configurations {
        // This configuration allows for test suites to properly depend on utility classes in other subprojects.
        // Dependencies should be included in the classpath when depending on testArtifacts in another subproject
        testArchives.extendsFrom testRuntime

        // These configurations allow for the integration test source set to be compiled and run as a test suite.
        // Integration tests should be able to get the same dependencies as the corresponding unit tests.
        intTestCompile.extendsFrom testCompile
        intTestRuntime.extendsFrom testRuntime
    }

    // this test jar is used to represent a test dependency for a subproject, since depending directly on a source set
    // does not guarantee that the source set was actually built.
    task testJar(type: Jar, dependsOn: testClasses) {
        classifier = 'test'
        from sourceSets.test.output
    }

    artifacts {
        // Depending on a jar instead of the source set directly prevents test subprojects from compiling before their
        // dependencies are built.
        testArchives testJar
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        testCompile "org.apache.logging.log4j:log4j-core:$log4jVersion"
        testCompile "org.apache.logging.log4j:log4j-api:$log4jVersion"
        testCompile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
        // Add PowerMock to support more unit test scenarios.
        // See https://github.com/powermock/powermock/wiki/Mockito for detail about PowerMock.
        testCompile "org.powermock:powermock-api-mockito2:$powermockVersion"
        testCompile "org.powermock:powermock-core:$powermockVersion"
        testCompile "org.powermock:powermock-module-junit4:$powermockVersion"
        testRuntime project(':log4j-test-config')
    }

    idea {
        // since integration tests are in a separate source set, we have to tell intellij about them
        module {
            testSourceDirs += sourceSets.intTest.java.srcDirs
            testResourceDirs += sourceSets.intTest.resources.srcDirs
            scopes.TEST.plus += [configurations.intTestCompile]
        }
    }

    test {
        testLogging {
            exceptionFormat = 'full'
            events "PASSED", "SKIPPED", "FAILED"
        }
        // Plugin for retrying flaky tests. Reference: https://github.com/gradle/test-retry-gradle-plugin
        retry {
            // The maximum number of times to retry an individual test
            maxRetries = 3
            // The maximum number of test failures that are allowed (per module) before retrying is disabled. The count applies to
            // each round of test execution. For example, if maxFailures is 5 and 4 tests initially fail and then 3
            // again on retry, this will not be considered too many failures and retrying will continue (if maxRetries {@literal >} 1).
            // If 5 or more tests were to fail initially then no retry would be attempted.
            maxFailures = 10
            // Whether tests that initially fail and then pass on retry should fail the task.
            failOnPassedAfterRetry = false
        }
        maxHeapSize = "3g"
        systemProperty 'io.netty.leakDetection.level', 'paranoid'
        systemProperty 'io.netty.allocator.tinyCacheSize', '0'
        systemProperty 'io.netty.allocator.smallCacheSize', '0'
        systemProperty 'io.netty.allocator.normalCacheSize', '0'
        systemProperty 'io.netty.allocator.maxCachedBufferCapacity', '0'

        // make sure Powermockito would be happy
        jvmArgs '--add-exports=java.xml/jdk.xml.internal=ALL-UNNAMED'
    }

    task intTest(type: Test) {
        description = 'Runs integration tests.'
        group = 'verification'
        testClassesDirs = sourceSets.intTest.output.classesDirs
        classpath = sourceSets.intTest.runtimeClasspath
        testLogging {
            exceptionFormat = 'full'
            events 'started', 'failed', 'passed', 'skipped'
            showStandardStreams = false
        }
        // Allow for retrying flaky integration tests.
        retry {
            // The maximum number of times to retry an individual test
            maxRetries = 3
            // The maximum number of test failures that are allowed (per module) before retrying is disabled. The count applies to
            // each round of test execution. For example, if maxFailures is 5 and 4 tests initially fail and then 3
            // again on retry, this will not be considered too many failures and retrying will continue (if maxRetries {@literal >} 1).
            // If 5 or more tests were to fail initially then no retry would be attempted.
            maxFailures = 10
            // Whether tests that initially fail and then pass on retry should fail the task.
            failOnPassedAfterRetry = false
        }
        maxHeapSize = "3g"
        systemProperty 'io.netty.leakDetection.level', 'paranoid'
    }

    task allTest {
        description = 'Runs unit and integration tests'
        group = 'verification'
    }
    allTest.dependsOn test
    allTest.dependsOn intTest

    javadoc {
        // TODO audit and fix our javadocs so that we don't need this setting
        // This is mainly for cases where param/throws tags don't have descriptions
        // Previously, javadocs weren't being compiled, but now shipkit automatically enables this build step
        options.addStringOption('Xdoclint:none', '-quiet') // Suppress lint warnings
        failOnError = false
    }
}

project(':ambry-utils') {
    dependencies {
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        compile "commons-codec:commons-codec:$commonsVersion"
        compile "org.json:json:$jsonVersion"
        compile "net.sf.jopt-simple:jopt-simple:$joptSimpleVersion"
        compile "io.netty:netty-all:$nettyVersion"
        testCompile project(":ambry-test-utils")
        testCompile "io.netty:netty-transport-native-epoll:$nettyVersion"
    }
}

project(':ambry-test-utils') {
    dependencies {
        compile "junit:junit:$junitVersion"
        compile "org.mockito:mockito-core:2.+"
        compile project(":ambry-utils")
        compile project(":ambry-commons")
        compile project(":ambry-clustermap")
        compile "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"
    }
}

project(':ambry-api') {
    dependencies {
        compile project(':ambry-utils')
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
        compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
        compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        testCompile project(':ambry-clustermap')
        testCompile project(':ambry-test-utils')
    }
}

project(':ambry-messageformat') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-utils')
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-api', configuration: 'testArchives')
    }
}

project(':ambry-commons') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-messageformat'),
                project(':ambry-utils')

        // See https://github.com/facebook/zstd/releases for Zstd releases.  On Jan 2022, latest release is 1.5.2
        // See https://repo.maven.apache.org/maven2/com/github/luben/zstd-jni/ for library versions.
        compile "com.github.luben:zstd-jni:$zstdVersion"

        // See https://github.com/lz4/lz4/releases for LZ4 releases.
        // There are JNI and Pure Java LZ4 provided at https://repo.maven.apache.org/maven2/org/lz4
        // The latest LZ4 library is 1.8.0 on 06/2021.
        compile "org.lz4:lz4-java:$lz4Version"

        compile "org.conscrypt:conscrypt-openjdk-uber:$conscryptVersion"
        compile "io.netty:netty-all:$nettyVersion"
        compile "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_linux_x86_64"
        compile "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_osx_aarch_64"
        compile "org.apache.helix:helix-core:$helixVersion"
        compile "com.github.ben-manes.caffeine:caffeine:3.1.1"
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-clustermap', configuration: 'testArchives')
    }
}


project(':ambry-account') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-mysql')
        compile "org.apache.helix:helix-core:$helixVersion"
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        compile "org.json:json:$jsonVersion"
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-clustermap', configuration: 'testArchives')
        testCompile "com.google.jimfs:jimfs:$jimFsVersion"
    }
}

project(':ambry-clustermap') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-commons'),
                project(':ambry-utils')
        compile "org.apache.helix:helix-core:$helixVersion"
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        compile "org.json:json:$jsonVersion"
        testCompile project(':ambry-commons')
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-api', configuration: 'testArchives')

    }
}

project(':ambry-network') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-clustermap')
        compile "io.netty:netty-all:$nettyVersion"
        compile "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_linux_x86_64"
        compile "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_osx_aarch_64"
        compile "io.netty:netty-transport-native-epoll:$nettyVersion"
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        testCompile project(':ambry-test-utils')
    }
}

project(':ambry-server') {
    dependencies {
        compile project(':ambry-clustermap'),
                project(':ambry-messageformat'),
                project(':ambry-network'),
                project(':ambry-protocol'),
                project(':ambry-commons'),
                project(':ambry-store'),
                project(':ambry-utils'),
                project(':ambry-rest'),
                project(':ambry-account'),
                project(':ambry-replication'),
                project(':ambry-mysql')
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        compile "io.dropwizard.metrics:metrics-jmx:$metricsVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-core:$log4jVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-api:$log4jVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        testCompile project(':ambry-router')
        testCompile project(':ambry-cloud')
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-cloud', configuration: 'testArchives')
        testCompile project(path: ':ambry-clustermap', configuration: 'testArchives')
        testCompile project(path: ':ambry-api', configuration: 'testArchives')
        testCompile project(path: ':ambry-replication', configuration: 'testArchives')
        testCompile project(path: ':ambry-messageformat', configuration: 'testArchives')
    }
}

project(':ambry-store') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-account'),
                project(':ambry-messageformat')
        compile "net.smacke:jaydio:$jaydioVersion"
        testCompile project(':ambry-clustermap')
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-clustermap', configuration: 'testArchives')
        testCompile project(path: ':ambry-api', configuration: 'testArchives')
    }

    test {
        testLogging {
            exceptionFormat = 'full'
            events 'started', 'failed', 'passed', 'skipped'
            showStandardStreams = false
        }
    }
}


project(':ambry-replication') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-protocol'),
                project(':ambry-store'),
                project(':ambry-network'),
                project(':ambry-clustermap')
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        testCompile project(':ambry-store')
        testCompile project(':ambry-clustermap')
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-clustermap', configuration: 'testArchives')
        testCompile project(path: ':ambry-api', configuration: 'testArchives')
        testCompile project(path: ':ambry-messageformat', configuration: 'testArchives')
        testCompile project(path: ':ambry-store', configuration: 'testArchives')
    }
}

project(':ambry-tools') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-account'),
                project(':ambry-store'),
                project(':ambry-server'),
                project(':ambry-rest'),
                project(':ambry-frontend'),
                project(':ambry-cloud')
        compile "io.dropwizard.metrics:metrics-jmx:$metricsVersion"
        compile "net.sf.jopt-simple:jopt-simple:$joptSimpleVersion"
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-clustermap', configuration: 'testArchives')
        testCompile project(path: ':ambry-account', configuration: 'testArchives')
        testCompile project(path: ':ambry-store', configuration: 'testArchives')
        testCompile project(path: ':ambry-api', configuration: 'testArchives')
    }
}

project(':ambry-protocol') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-clustermap'),
                project(':ambry-messageformat'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-network')
        testCompile project(':ambry-test-utils')
    }
}

project(':ambry-rest') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-account'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-quota'),
                project(':ambry-network')
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        compile "io.dropwizard.metrics:metrics-jmx:$metricsVersion"
        compile "net.sf.jopt-simple:jopt-simple:$joptSimpleVersion"
        compile "io.netty:netty-all:$nettyVersion"
        compile "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_linux_x86_64"
        compile "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_osx_aarch_64"
        compile "io.netty:netty-transport-native-epoll:$nettyVersion"
        compile "javax.servlet:javax.servlet-api:$javaxVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-core:$log4jVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-api:$log4jVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-api', configuration: 'testArchives')
        testCompile project(path: ':ambry-account', configuration: 'testArchives')
        testCompile "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"
    }
}

project(':ambry-router') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-clustermap'),
                project(':ambry-messageformat'),
                project(':ambry-protocol'),
                project(':ambry-network'),
                project(':ambry-cloud'),
                project(':ambry-rest')
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        compile "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-network', configuration: 'testArchives')
        testCompile project(path: ':ambry-cloud', configuration: 'testArchives')
    }
}

project(':ambry-frontend') {
    dependencies {
        compile project(':ambry-api'),
                project(':ambry-account'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-rest'),
                project(':ambry-router'),
                project(':ambry-quota')
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        testCompile project(':ambry-test-utils')
        testCompile project(':ambry-quota')
        testCompile project(':ambry-named-mysql')
        testCompile project(path: ':ambry-api', configuration: 'testArchives')
        testCompile project(path: ':ambry-rest', configuration: 'testArchives')
    }
}

project(':ambry-cloud') {
    dependencies {
        compile project(':ambry-api'),
            project(':ambry-utils'),
            project(':ambry-commons'),
            project(':ambry-account'),
            project(':ambry-replication'),
            project(':ambry-rest')
        compile "com.azure:azure-storage-blob:$azureStorageBlobVersion"
        compile "com.azure:azure-storage-blob-batch:$azureStorageBlobBatchVersion"
        compile "com.azure:azure-identity:$azureIdentityVersion"
        compile "com.azure:azure-security-keyvault-secrets:$azureSecurityKeyvaultSecretsVersion"
        compile "io.dropwizard.metrics:metrics-core:$metricsVersion"
        compile "io.dropwizard.metrics:metrics-jmx:$metricsVersion"
        compile "com.azure:azure-cosmos:$azureCosmosDbAsyncVersion"
        compile "com.microsoft.azure:msal4j:$azureMsal4jVersion"
        compile "org.apache.helix:helix-lock:$helixVersion"
        testCompile project(':ambry-router')
        testCompile project(':ambry-store')
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-clustermap', configuration: 'testArchives')
        testCompile project(path: ':ambry-store', configuration: 'testArchives')
        testCompile project(path: ':ambry-replication', configuration: 'testArchives')
    }
}

project(':ambry-named-mysql') {
    dependencies {
        compile project(':ambry-api')
        compile project(':ambry-mysql')
        compile project(':ambry-commons')
        compile "mysql:mysql-connector-java:$mysqlConnectorVersion"
        compile "com.zaxxer:HikariCP:$hikariVersion"
        testCompile project(':ambry-test-utils')
    }
}

project(':ambry-quota') {
    dependencies {
        compile project(':ambry-api'),
            project(':ambry-commons'),
            project(':ambry-mysql'),
            project(':ambry-clustermap')
        compile "org.apache.helix:helix-core:$helixVersion"
        testCompile project(':ambry-test-utils')
        testCompile project(':ambry-account')
        testCompile project(path: ':ambry-clustermap', configuration: 'testArchives')
    }
}

project(':ambry-mysql') {
    dependencies {
        compile project(':ambry-api')
        compile project(':ambry-utils')
        compile project(':ambry-commons')
        compile "mysql:mysql-connector-java:$mysqlConnectorVersion"
        compile "org.apache.helix:helix-core:$helixVersion"
        compile "com.zaxxer:HikariCP:$hikariVersion"
        testCompile project(':ambry-test-utils')
        testCompile project(path: ':ambry-clustermap', configuration: 'testArchives')
        testCompile project(path: ':ambry-api', configuration: 'testArchives')
    }
}

project(':ambry-all') {
    dependencies {
        // this is a meta project that depends on all of the "entry-point" subprojects to make it easier to pull in the
        // entire dependency tree.
        compile project(':ambry-frontend')
        compile project(':ambry-server')
        compile project(':ambry-tools')
        compile project(':ambry-cloud')
        compile project(':ambry-named-mysql')
        compile project(':ambry-test-utils')
        compile project(':ambry-quota')
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path)}

task allJar(type: Jar, dependsOn: subprojects.assemble) {
    manifest {
        attributes 'Implementation-Title': 'Ambry',
                   'Main-Class': 'com.github.ambry.server.AmbryMain'
    }
    zip64 true
    destinationDir = file('target')
    baseName = 'ambry'
    subprojects.each { subproject ->
        from  {
            (subproject.configurations.archives.allArtifacts.files + subproject.configurations.runtime).collect {
                zipTree(it)
            }
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

task allJarVcr(type: Jar, dependsOn: subprojects.assemble) {
    manifest {
        attributes 'Implementation-Title': 'Vcr',
                   'Main-Class': 'com.github.ambry.cloud.VcrMain'
    }
    zip64 true
    destinationDir = file('target')
    baseName = 'ambry-vcr'
    subprojects.each { subproject ->
        from  {
            (subproject.configurations.archives.allArtifacts.files + subproject.configurations.runtime).collect {
                zipTree(it)
            }
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

task codeCoverageReport(type: JacocoReport) {
    // This task will compile a report for whatever tests have been run.
    // Make sure to run the desired test tasks before generating the coverate report.
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    // always rerun task when requested to get new execution data
    outputs.upToDateWhen { false }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled true
        csv.enabled true
    }
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
